import { split } from "std/text"
import { echo_info } from "../lib/echo.ab"
import { debug } from "../lib/debug.ab"
import { sed_inplace } from "../lib/sed.ab"
import { urlencode_npm_pkg } from "../lib/cmd.ab"

pub fun latest_npm_version(name: Text): Text {
    let enc = urlencode_npm_pkg(name)
    let result = trust $ curl -sL "https://registry.npmjs.org/{enc}" | jq -r '."dist-tags".latest' $
    return result
}

pub fun process_npm_file(ref update_flag: Bool, file: Text): Null {
    let packages_block = trust $ awk '/packages[[:space:]]*=[[:space:]]*\[/\{flag=1;next}/\]/\{flag=0}flag' "{file}" | sed -n 's/.*"\(.*\)".*/\1/p' $
    let pkgs = split(packages_block, "\n")

    debug("npm pkgs parsed from {file}: {len(pkgs)} items")

    let changed = false

    for i, full in pkgs {
        if full == "" {
            continue
        }
        let base = trust $ echo "{full}" | sed 's/@[^@]*\$//' $
        let ver = trust $ echo "{full}" | sed 's/.*@//' $

        if base == full {
            continue
        }

        let latest = latest_npm_version(base)

        if latest != "" and latest != ver {
            let sed_script = "s|\"{base}@[^\"]*\"|\"{base}@{latest}\"|"
            debug("npm sed: base={base} ver={ver} latest={latest}")
            trust sed_inplace(sed_script, file)
            changed = true
            update_flag = true
            echo "Updated npm {base}: {ver} -> {latest} in {file}"
        }
    }

    if changed {
        let script_dir = trust $ cd "\$(dirname "\$0")" && pwd $
        let build_pkg = "{script_dir}/build-pkg"
        echo_info("Running build-pkg for {file}...")
        trust $ "{build_pkg}" "{file}" $
    }
}
