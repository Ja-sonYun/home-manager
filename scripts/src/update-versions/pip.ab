import { split, text_contains, starts_with } from "std/text"
import { echo_info } from "../lib/echo.ab"
import { debug } from "../lib/debug.ab"
import { sed_inplace } from "../lib/sed.ab"

pub fun latest_pypi_version(name: Text): Text {
    let result = trust $ curl -sL "https://pypi.org/pypi/{name}/json" | jq -r '.info.version' $
    return result
}

pub fun process_pip_file(ref update_flag: Bool, file: Text): Null {
    let packages_block = trust $ awk '/packages[[:space:]]*=[[:space:]]*\[/\{flag=1;next}/\]/\{flag=0}flag' "{file}" | sed -n 's/.*"\(.*\)".*/\1/p' $
    let pkgs = split(packages_block, "\n")

    debug("pip pkgs parsed from {file}: {len(pkgs)} items")

    let changed = false

    for i, full in pkgs {
        if full == "" {
            continue
        }

        if starts_with(full, "git+") {
            continue
        }

        if text_contains(full, "==") {
            let name = trust $ echo "{full}" | sed 's/==.*//' $
            let ver = trust $ echo "{full}" | sed 's/.*==//' $

            let latest = latest_pypi_version(name)

            if latest != "" and latest != ver {
                let pscript = "s|\"{name}==[^\"]*\"|\"{name}=={latest}\"|"
                debug("pip sed: name={name} ver={ver} latest={latest}")
                trust sed_inplace(pscript, file)
                changed = true
                update_flag = true
                echo "Updated pip {name}: {ver} -> {latest} in {file}"
            }
        }
    }

    if changed {
        let script_dir = trust $ cd "\$(dirname "\$0")" && pwd $
        let build_pkg = "{script_dir}/build-pkg"
        echo_info("Running build-pkg for {file}...")
        trust $ "{build_pkg}" "{file}" $
    }
}
