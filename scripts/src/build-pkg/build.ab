import { text_contains } from "std/text"
import { echo_success, echo_warning, echo_error } from "../lib/echo.ab"
import { debug } from "../lib/debug.ab"
import { sed_inplace } from "../lib/sed.ab"
import { get_hashfile_path, get_hostname } from "./config.ab"

pub fun nix_build(root_dir: Text, file: Text, key: Text): Text {
    let hostname = get_hostname()

    let abs_file = file
    let is_abs = trust $ echo "{file}" | grep -q "^/" && echo "1" || echo "0" $
    if is_abs == "0" {
        abs_file = "{root_dir}/{file}"
    }

    debug("Building: {abs_file} (key={key}, hostname={hostname})")

    let tmpbase = trust $ mktemp -t build-pkg $
    let tmpfile = "{tmpbase}.nix"
    trust $ mv "{tmpbase}" "{tmpfile}" $

    let script_dir = trust $ cd "\$(dirname "\$0")" && pwd $
    let template = "{script_dir}/src/build-pkg/template.nix"

    trust $ cp "{template}" "{tmpfile}" $
    trust sed_inplace("s|__HOSTNAME__|{hostname}|g", tmpfile)
    trust sed_inplace("s|__ROOT_DIR__|{root_dir}|g", tmpfile)
    trust sed_inplace("s|__FILE__|{abs_file}|g", tmpfile)
    debug("Nix file: {tmpfile}")

    let outfile = trust $ mktemp -t build-pkg-out $
    trust $ nix build --impure --file "{tmpfile}" -L > "{outfile}" 2>&1 || true $
    let out = trust $ cat "{outfile}" $
    trust $ rm -f "{tmpfile}" "{outfile}" $
    return out
}

pub fun extract_hash_from_output(out: Text): Text {
    let hash = trust $ echo "{out}" | sed -n 's/.*got: *\(sha256-[A-Za-z0-9+/=]\+\).*/\1/p' | tail -n1 $
    return hash
}

pub fun update_hashfile(root_dir: Text, key: Text, hash: Text): Bool {
    let hashfile = get_hashfile_path(root_dir)
    let hostname = get_hostname()

    debug("Updating hashfile: {hashfile}")
    debug("Hostname: {hostname}, Key: {key}, Hash: {hash}")

    let updated = trust $ jq --arg host "{hostname}" --arg key "{key}" --arg hash "{hash}" '.[\$host][\$key] = \$hash' "{hashfile}" $

    if updated == "" {
        return false
    }

    trust $ echo "{updated}" > "{hashfile}" $
    return true
}

pub fun build_package(root_dir: Text, file: Text, key: Text): Bool {
    let out = nix_build(root_dir, file, key)
    let has_hash = text_contains(out, "got:")
    let has_error = text_contains(out, "error:")

    if has_hash {
        let new_hash = extract_hash_from_output(out)
        if new_hash == "" {
            echo_error("Failed to extract hash from build output")
            return false
        }

        echo_warning("Hash mismatch detected. New hash: {new_hash}")

        if not update_hashfile(root_dir, key, new_hash) {
            echo_error("Failed to update hashfile")
            return false
        }

        echo_success("Updated hashfile.json with new hash")

        let rebuild_out = nix_build(root_dir, file, key)
        if text_contains(rebuild_out, "got:") {
            echo_error("Rebuild still failed after hash update")
            trust $ echo "{rebuild_out}" >&2 $
            return false
        }

        if text_contains(rebuild_out, "error:") {
            echo_error("Build failed with error")
            trust $ echo "{rebuild_out}" >&2 $
            return false
        }

        echo_success("Rebuild successful!")
        return true
    }

    if has_error {
        echo_error("Build failed")
        debug("Output: {out}")
        return false
    }

    echo_success("Build successful (no hash update needed)")
    return true
}
